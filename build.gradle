plugins {
    id 'java'
    id 'io.freefair.lombok' version '8.4'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = 'io.github.metriximor'
version = '0.3.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven { url "https://maven.enginehub.org/repo/" }
    maven { url 'https://jitpack.io' }
}

dependencies {
    // Minecraft
    //noinspection VulnerableLibrariesLocal
    compileOnly(testImplementation("io.papermc.paper:paper-api:1.18.2-R0.1-SNAPSHOT"))

    // Math
    implementation "com.github.Jilocasin:nearest-neighbour:308a6107f6379dae3fadcdceb01e3dfce5d3f317"

    // Testing
    testImplementation "org.mockito:mockito-core:3.+"
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

test {
    useJUnitPlatform()
}